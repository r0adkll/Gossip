apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

gitVersioner {
    baseBranch 'master'
    yearFactor 1200
    addSnapshot true
    addLocalChangesDetails true

    formatter = { gitVersioner ->
        return "${VERSION_NAME}.${GitVersioner.DEFAULT_FORMATTER(gitVersioner)}"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.0"

    defaultConfig {
        applicationId "com.r0adkll.gossip"

        minSdkVersion 22
        targetSdkVersion 29

        versionCode gitVersioner.versionCode
        versionName gitVersioner.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        alpha {

        }

        beta {

        }

        prod
    }

    lintOptions {
        xmlReport true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ["-Xallow-result-return-type"]
    }

    aaptOptions {
        noCompress "tflite"
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests {
        includeAndroidResources = true

        all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == "release") {
                outputFileName = "Gossip-" + variant.versionName + ".apk"
            }
        }
    }
}

androidExtensions {
    experimental = true
}

ext {
    androidXCoreVersion = '1.2.0-alpha04'
    androidXTestCoreVersion = '1.2.1-alpha02'
    androidXTestExtKotlinRunnerVersion = '1.1.2-alpha02'
    androidXTestRulesVersion = '1.3.0-alpha02'
    androidXTestRunnerVersion = '1.3.0-alpha02'
    appCompatVersion = '1.1.0'
    constraintLayoutVersion = '2.0.0-beta2'
    coroutinesVersion = '1.3.0-RC2'
    espressoVersion = '3.3.0-alpha02'
    ftKitVersion = '2.0.3'
    fragmentVersion = '1.2.0-alpha03'
    junitVersion = '4.12'
    kakaoVersion = '2.1.0'
    koinVersion = '2.0.1'
    lifecycleVersion = '2.2.0-alpha04'
    materialVersion = '1.1.0-alpha10'
    mockitoVersion = '3.0.0'
    navVersion = '2.2.0-alpha02'
    timberVersion = '4.7.1'
    truthVersion = '0.44'
    recyclerViewVersion = '1.1.0-beta04'
    robolectricVersion = '4.3'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.core:core-ktx:$androidXCoreVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"
    implementation "androidx.recyclerview:recyclerview:$recyclerViewVersion"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"
    implementation "com.ftinc:kit:$ftKitVersion"
    implementation "com.jakewharton.timber:timber:$timberVersion"

    /*
     * TODO: Insert your dependencies here
     */

    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    implementation "androidx.navigation:navigation-fragment-ktx:$navVersion"
    implementation "androidx.navigation:navigation-ui-ktx:$navVersion"
    implementation "androidx.test.espresso:espresso-idling-resource:$espressoVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"
    implementation 'io.coil-kt:coil:0.6.1'

    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    implementation 'com.google.firebase:firebase-firestore-ktx:21.1.1'
    implementation 'com.google.firebase:firebase-messaging:20.0.0'
    implementation 'com.google.firebase:firebase-storage:19.0.1'
    implementation 'com.google.firebase:firebase-auth:19.0.0'
    implementation 'com.google.firebase:firebase-ml-natural-language:21.0.2'
    implementation 'com.google.firebase:firebase-ml-natural-language-smart-reply-model:20.0.5'
    implementation 'com.google.firebase:firebase-perf:19.0.0'
    implementation 'com.firebaseui:firebase-ui-auth:4.3.1'

    /*
     * TODO: END your dependencies here. Be sure to delete these blocks
     */

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.koin:koin-test:$koinVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    testImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    testImplementation "androidx.test:core-ktx:$androidXTestCoreVersion"
    testImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
    testImplementation "androidx.test:rules:$androidXTestRulesVersion"
    testImplementation "com.google.truth:truth:$truthVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    androidTestImplementation "org.koin:koin-test:$koinVersion"
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "androidx.test:runner:$androidXTestRunnerVersion"
    androidTestImplementation "androidx.test.ext:junit-ktx:$androidXTestExtKotlinRunnerVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espressoVersion"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espressoVersion"
    androidTestImplementation "com.agoda.kakao:kakao:$kakaoVersion"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    androidTestImplementation "org.robolectric:annotations:$robolectricVersion"
    debugImplementation ("androidx.fragment:fragment-testing:$fragmentVersion") {
        exclude group: 'androidx.test', module: 'core'
    }
}

apply plugin: 'com.google.gms.google-services'
